plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id("org.jooq.jooq-codegen-gradle") version "3.20.10"
    id("com.gradleup.shadow") version "9.3.0"
}

group = 'com.venuss'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.8-R0.1-SNAPSHOT")
    implementation("org.jooq:jooq:3.20.9")
    implementation("org.mariadb.jdbc:mariadb-java-client:3.5.3")
    implementation("com.zaxxer:HikariCP:7.0.2")
    implementation("net.kyori:adventure-text-minimessage:4.25.0")
    jooqCodegen 'org.mariadb.jdbc:mariadb-java-client:3.4.1'
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21")
    }
}

jooq {
    configuration {
        jdbc {
            driver = "org.mariadb.jdbc.Driver"
            url = "jdbc:mariadb://localhost:3306/smpcore"
            user = "smpcore"
            password = "smpcore"
        }
        generator {
            name = "org.jooq.codegen.DefaultGenerator"
            database {
                name = "org.jooq.meta.mariadb.MariaDBDatabase"
                inputSchema = "smpcore"
            }
            target {
                packageName = "com.venuss.smpcore.jooq.generated"
                directory = "src/main/java"
            }
        }
    }
}
def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}
